plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.15.1'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    testImplementation 'junit:junit:[4.13.2,)'
}

group = 'com.github.javadev'
version = '1.3'
description = 'leetcode-in-java'
java.sourceCompatibility = JavaVersion.VERSION_1_8

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

spotless {
    java {
        encoding 'UTF-8'
        target fileTree(projectDir) {
            include '**/*.java'
            exclude '**/build/**'
        }
        importOrder '\\#', '', '*'
        removeUnusedImports()
        googleJavaFormat('1.7').aosp()
        toggleOffOn()
        endWithNewline()
    }
}
